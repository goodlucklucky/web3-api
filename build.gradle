buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:4.1.0'
    }
}

plugins {
    id 'de.undercouch.download' version '4.1.2'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
}

description = 'Web3j-API code generation for Ethereum smart contracts.'

ext {
    web3jVersion = version
}

subprojects {
    apply plugin: "kotlin"
    apply from: "$rootProject.projectDir/gradle/versions.gradle"

    repositories {
        mavenCentral()
        maven { url "https://artifacts.consensys.net/public/maven/maven/" }
    }

    apply {
        [
                'publish',
                'jacoco',
                'java',
                'javadoc',
                'junit',
                'repositories',
                'spotless'
        ].each { buildScript ->
            download {
                src "https://raw.githubusercontent.com/hyperledger/web3j-build-tools/main/gradle/$buildScript/build.gradle"
                dest "$rootDir/gradle/$buildScript/build.gradle"
                overwrite false
                quiet true
                onlyIfModified true
            }
            from("$rootDir/gradle/$buildScript/build.gradle")
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    scm {
                        url = "https://github.com/web3j/web3j-api"
                        connection = "scm:https://github.com/web3j/web3j-api.git"
                        developerConnection = "scm:git://github.com/web3j/web3j-api.git"
                    }
                }
            }
        }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
        useJUnitPlatform()
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "io.github.microutils:kotlin-logging:${versions.kotlinLogging}"
        implementation "ch.qos.logback:logback-classic:${versions.logback}"
        implementation "org.slf4j:slf4j-api:${versions.slf4j}"
        implementation "org.slf4j:jul-to-slf4j:${versions.slf4j}"

        testImplementation "com.willowtreeapps.assertk:assertk-jvm:${versions.assertk}"
        testImplementation "org.mockito:mockito-junit-jupiter:${versions.mockitoJunit}"

        configurations.all {
            resolutionStrategy {
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-base', version: versions.jackson)
                force(group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: versions.jackson)
                force(group: 'io.github.microutils', name: 'kotlin-logging', version: versions.kotlinLogging)
                force(group: 'io.swagger.core.v3', name: 'swagger-core', version: versions.swagger)
                force(group: 'io.swagger.core.v3', name: 'swagger-models', version: versions.swagger)
                force(group: 'io.swagger.core.v3', name: 'swagger-annotations', version: versions.swagger)
                force(group: "org.jetbrains.kotlin", name: "kotlin-compiler-embeddable", version: kotlinVersion)
                force(group: "org.jetbrains.kotlin", name: "kotlin-script-runtime", version: kotlinVersion)
            }
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "17"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "17"
    }

    sourceCompatibility = 17

    tasks.named("spotlessJava") {
        dependsOn(tasks.named("spotlessGroovyGradle"))
    }
}
